<root>			::=		<ACTIONS> 

<ACTIONS>		::=		<ACTION>+
<ACTION>		::=		( <BLANK_SCREEN> | <TITLE> | <STACK> | <IMPORT_CLIP> | <SUBTITLE> | <CONCAT> | <EXPORT> | <ROGNAGE> | <DEFINE_PATH> | <ADD_SHAPE> | <ADD_MASK> | <SET_GLOBAL_COLOR> )

<BLANK_SCREEN>	::=		"clip" <CLIP_IDENTIFIER> "color" <COLOR> "background" <TEMPORAL>?

<ADD_SHAPE>		::=		"add" "shape" <WORD_CHARACTER>+ "type"
						<WORD_CHARACTER>+ <PROPERTY>+
<PROPERTY>		::=		<WORD_CHARACTER>+ (<INTEGER> | <FLOAT>)

<ADD_MASK>		::=		"add" <WORD_CHARACTER>+ "mask" <WORD_CHARACTER>+
						"with" "shape" <WORD_CHARACTER>+
						"with" "verticalPosition" <FLOAT> "and"
						"horizontalPosition" <FLOAT> <TEMPORAL>
						("outsideShape")?
<SET_GLOBAL_COLOR> ::=	"setGlobalColorTheme" <COLOR>

<TITLE>			::=		"add" "title" <TITLE_TEXT> "to" <CLIP_IDENTIFIER>
						<PATH>?
<SUBTITLE>		::=		"add" "subtitle" <TITLE_TEXT> ( "color" <COLOR> )?
						( "fontsize" ([0-9]+px) )? "to" <CLIP_IDENTIFIER>
						<TEMPORAL> <PATH>?
<PATH>			::=		"with" "path" <WORD_CHARACTER> "and" "speed" <INTEGER>

<DEFINE_PATH>	::=		"define" "path" <WORD_CHARACTER> <POSITION>+ "end" "define"
<POSITION>		::=		"verticalPosition" <FLOAT> "horizontalPosition" <FLOAT>


<TEMPORAL>		::=		( "at" <TIME> )? "during" <TIME>

<STACK>			::=		"add" "clip" <CLIP_IDENTIFIER> "on" ( "top" | "bottom" ) ( "left" | "right" ) <SCALE> "to" <CLIP_IDENTIFIER>
<SCALE>			::=		"scale" <FLOAT>

<IMPORT_CLIP>	::=		"clip"	<CLIP_IDENTIFIER> "is" <FILENAME> <EXTRACT_TIME>?	
<EXTRACT_TIME>	::=		"from" <TIME> "to" <TIME>

<CONCAT>		::=		<CLIP_IDENTIFIER> ( and <CLIP_IDENTIFIER> )+ <TRANSITION>? "to" <CLIP_IDENTIFIER>
<TRANSITION>	::=		"with" "transition" [a-zA-Z]+

<EXPORT>		::=		"export" <CLIP_IDENTIFIER> "as" <FILENAME>

<ROGNAGE>		::=		"rognage" <CLIP_IDENTIFIER> "border" "top" <FLOAT>
						"bottom" <FLOAT> "left" <FLOAT> "right" <FLOAT>

<TITLE_TEXT>		::=		""" ( <WORD_CHARACTER> | <WHITESPACE> | [.!:,'] )+ """
<INTEGER>			::=		[0-9]+
<FLOAT>				::=		[0-9]+ "." [0-9]+
<FILENAME>			::=		""" <WORD_CHARACTER>+ "." <WORD_CHARACTER>+ """
<TIME>				::=		"-"? ( [0-9]+h )? ( [0-9]+m )? [0-9]+s
<CLIP_IDENTIFIER>	::=		<WORD_CHARACTER>+
<COLOR>				::=		"#"[0-9a-f]+
<WORD_CHARACTER>	::=		[a-zA-Z0-9]
<WHITESPACE>		::=		((" " | "\t")+)
