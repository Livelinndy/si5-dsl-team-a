<root>              ::= <actions>

<actions>           ::= ( <init> | <add-sth> | <concat> | <export> )*

<init>              ::= "clip" <IDENTIFIER> ( <video> | <blank> )

<video>             ::= "is" <MP4> <subclip>?

<subclip>           ::= "from" <TIME> "to" <TIME>

<blank>             ::= <background> <duration>

<background>        ::= <color> "background"

<color>             ::= "color" <HEXCOLOR>

<duration>          ::= "during" <TIME>

<add-sth>           ::= "add" ( <add-text> | <add-clip-on-top> )

<add-text>          ::= <text-type> <TEXT> ( "at" ( <TIME> | "end" ) <duration> )?

<text-type>         ::= ( "title" | "subtitle" | ("text" <POSITION> <NUMBER>? ) )

<add-clip-on-top>   ::= "clip" "on" <POSITION> "scale" <RATIO> "to" <IDENTIFIER>

<concat>            ::= "concat" <IDENTIFIER> "and" <IDENTIFIER> <concat-additional>* <with-transition>? "to" <IDENTIFIER>

<concat-additional> ::= "and" <IDENTIFIER>

<with-transition>   ::= "with" "transition" <transition-type>

<transition-type>   ::= "FADE"

<export>            ::= "export" <IDENTIFIER> "as" <MP4>

<MP4>				::= """ <IDENTIFIER> ".mp4""

<POSITION>			::= <POS_X>? <POS_Y>?
<POS_X>				::= "left" | "center" | "right"
<POS_Y>	 			::= "top" | "center" | "bottom"

<HEXCOLOR>			::= "#" [0-9abcdef]{3,6}

<TIME>				::= "-"? ( ( "\d{1,2}h") ? "\d{1,2}m" )? "\d{1,2}s"
<TEXT>				::= """ ( <IDENTIFIER> | <WHITESPACE> )+ """
<RATIO>				::= ( "0" | "1" ) "." <DIGIT>

<NUMBER>			::= <DIGIT>+
<IDENTIFIER>		::= ( <LOWERCASE> | <UPPERCASE> ) ( <LOWERCASE> | <UPPERCASE> | <DIGIT> )+
<LOWERCASE>			::= [a-z]
<UPPERCASE>			::= [A-Z]
<DIGIT>				::= [0-9]

<NEWLINE>			::= ("\r"? "\n" | "\r")+		-> skip
<WHITESPACE>		::= ((" " | "\t")+)			-> skip
<COMMENT>			::= "//" ~( "\r" | "\n" )*		-> skip
