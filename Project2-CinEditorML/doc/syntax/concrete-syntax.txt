<root>				::= <initialization> <actions>

<initialization>		::= ("clip" <IDENTIFIER> ( <import> | <declaration> ) )+
<import>			::= "is" <videofilename> <optional-cut>?
<declaration>			::= <background> <duration>

<optional-cut>			::= "from" <time> "to" <time>

<background>			::= <color> "background"
<duration>			::= "during" <time>

<actions>			::= ( <add-something> | <concat> | <export> )*

<add-something>			::= "add" ( <add-text> | <add-clip-on-top> )
<add-text>			::= ("title" | "subtitle" | ("text" <position> <textsize> )) <text> ("at" <time> <duration>)?
<add-clip-on-top>		::= "clip" "on" <position> "scale" <ratio> "to" <IDENTIFIER>

<concat>				::= "concat" <IDENTIFIER> "and" <IDENTIFIER> <concat-additional>* <with-transition>? "to" <IDENTIFIER>
<concat-additional>		::= "and" <IDENTIFIER>

<with-transition>		::= "with" "transition" <transition-type>
<transition-type>		::= "fade"

<export>			::= "export" <IDENTIFIER> "as" <videofilename>

<color>				::= "color" <hexcolor>
<hexcolor>			::= "#" [0-9abcdef]{3,6}

<videofilename>			::= """ <IDENTIFIER> ".mp4""

<position>			::= <horizontal-position> <vertical-position>
<horizontal-position>		::= "left" | "center" | "right"
<vertical-position>	 	::= "top" | "center" | "bottom"

<time>				::= "-"? ( ( "\d{1,2}h") ? "\d{1,2}m" )? "\d{1,2}s"

<textsize>			::= <DIGIT>+

<text>				::= """ ( <IDENTIFIER> | <WHITESPACE> )+ """

<ratio>				::= ( "0" | "1" ) "." <DIGIT>

<IDENTIFIER>			::= ( <LOWERCASE> | <UPPERCASE> ) ( <LOWERCASE> | <UPPERCASE> | <DIGIT> )+
<LOWERCASE>			::= [a-z]
<UPPERCASE>			::= [A-Z]
<DIGIT>				::= [0-9]

<NEWLINE>			::= ("\r"? "\n" | "\r")+		-> skip
<WHITESPACE>			::= ((" " | "\t")+)			-> skip
<COMMENT>			::= "//" ~( "\r" | "\n" )*		-> skip